buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.3.2'
    }
}

plugins {
    id 'java'
    id 'application'
}

version = 'v3.5.0'
group = 'net.xiaoyu233'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    maven {
        name = "Spongepowered"
        url = "https://repo.spongepowered.org/repository/maven-public/"
    }
    maven {
        name = 'legacy-fabric'
        url = 'https://maven.legacyfabric.net'
    }
    maven {
        name = 'fabric'
        url = 'https://maven.fabricmc.net'
    }
    maven {
        name = 'Minecraft'
        url = 'https://libraries.minecraft.net'
    }
}

configurations {
    shaded
    implementation.extendsFrom shaded
}

dependencies {
    implementation files('libs/1.6.4-MITE.jar')

    shaded 'com.google.code.gson:gson:2.11.0'
    shaded 'com.google.guava:guava:33.4.8-jre'

    shaded 'net.fabricmc:access-widener:2.1.0'
    shaded 'net.fabricmc:tiny-remapper:0.9.0'
    shaded 'net.fabricmc:mapping-io:0.7.1'

    shaded 'org.apache.logging.log4j:log4j-api:2.25.1'
    shaded 'org.apache.logging.log4j:log4j-core:2.25.1'
    shaded 'org.apache.commons:commons-lang3:3.18.0'

//    shaded 'org.lwjgl.lwjgl:lwjgl:2.9.4-nightly-20150209'
//    shaded 'org.lwjgl.lwjgl:lwjgl_util:2.9.4-nightly-20150209'
//    shaded 'org.lwjgl.lwjgl:lwjgl-platform:2.9.4-nightly-20150209'
    shaded 'org.lwjgl.lwjgl:lwjgl:2.9.0'
    shaded 'org.lwjgl.lwjgl:lwjgl_util:2.9.0'
    shaded 'org.lwjgl.lwjgl:lwjgl-platform:2.9.0'

    shaded 'org.ow2.asm:asm:9.6'
    shaded 'org.ow2.asm:asm-analysis:9.6'
    shaded 'org.ow2.asm:asm-commons:9.6'
    shaded 'org.ow2.asm:asm-util:9.6'
    shaded 'org.ow2.asm:asm-tree:9.6'

    shaded 'org.ow2.sat4j:org.ow2.sat4j.pb:2.3.6'
    shaded 'org.ow2.sat4j:org.ow2.sat4j.core:2.3.6'

    implementation 'it.unimi.dsi:fastutil:8.5.12'

}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

task fatJar(type: Jar) {
    archiveClassifier = 'all'

    from sourceSets.main.output

    from {
        configurations.shaded.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        duplicatesStrategy = DuplicatesStrategy.WARN
        // 排除签名文件
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.RSA'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*LICENSE*'
        exclude 'META-INF/*NOTICE*'
    }

    manifest {
        attributes 'Main-Class': 'net.xiaoyu233.fml.relaunch.DevLaunchInjector'
    }
}

task launchMinecraft(type: JavaExec, dependsOn: fatJar) {
    mainClass = 'net.xiaoyu233.fml.relaunch.client.Main'
    classpath = files(fatJar.archiveFile)
    jvmArgs '-Dminecraft.client.jar=libs/1.6.4-MITE.jar'
}